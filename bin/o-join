#!/usr/bin/perl

# Copyright (c) 2019 Todd T. Fries <todd@fries.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;

use Data::Dumper;

if (!open(OA,"data/o-abilities")) {
	print "o-abilities can't be opened, ugh\n";
	exit(1);
}

our $max = { };

our @officers;

my @flagslist = (
	'player', 'hostile', 'station', 'mining', 'dodge', 'shield', 'armor', 'hull health',
	'interceptor', 'battleship', 'explorer', 'surveyor',
	'hunting', 'mining', 'miner', 'hunter',
	'ship',
	'tritanium', 'dilithium', 'parsteel',
);

# translate lhs into rhs flag name (alias?)
our $flagtr = { };
$flagtr->{miner} = 'mining';
$flagtr->{hunter} = 'hunting';

# auto add additional flags
our $flagadd = { };
@{$flagadd->{player}} = ('dodge', 'shield', 'armor', 'hull health');
@{$flagadd->{hostile}} = ('dodge', 'shield', 'armor', 'hull health');

# one flag excludes other
our $flagexcl = { };
@{$flagexcl->{player}} = ('hostile', 'station', 'mining');
@{$flagexcl->{hostile}} = ('player', 'station', 'mining');
@{$flagexcl->{station}} = ('player', 'hostile', 'mining');
@{$flagexcl->{mining}} = ('player', 'hostile', 'station', 'hunting');
@{$flagexcl->{hunting}} = ('mining', 'station');
@{$flagexcl->{parsteel}} = ('tritanium', 'dilithium');
@{$flagexcl->{tritanium}} = ('parsteel', 'dilithium');
@{$flagexcl->{dilithium}} = ('parsteel', 'tritanium');

my $l;
while(<OA>) {
	chomp($l=$_);
	if ($l =~ /^([A-Z][a-z']+)\s+(\S+)\s+(.+)\s+([\+\-]\s?[0-9]+%\s.*)$/) {
		my $o = { };
		$o->{pos} = $1;
		$o->{oname} = $2;
		$o->{aname} = $3;
		$o->{desc} = $4;
		$o->{aname} =~ s/\s+$//;
		foreach my $flag (@flagslist) {
			my $freal = $flag;
			if (defined($flagtr->{$flag})) {
				$freal = $flagtr->{$flag};
			}
			if (!defined($o->{flags}->{$freal})) {
				$o->{flags}->{$freal} = 0;
			}
			foreach my $i (('aname', 'desc')) {
				if ($o->{$i} =~ /${flag}/i) {
					print $o->{oname}." flag ".${freal}."++\n";
					$o->{flags}->{$freal}++;
				}
			}
		}
		#print Dumper($o);
		push @officers,$o;
		check_max($o, 'aname');
		check_max($o, 'oname');
		next;
	}
	print "Unhandled line: $l\n";
}
close(OA);

my $onmax = $max->{oname};
my $anmax = $max->{aname};
my $ofmt = "%3s %-${onmax}s %-${anmax}s %s\n";

my $lname = "";
my $ocmanable = "";
foreach my $o (sort
	{
		if ($a->{oname} eq $b->{oname}) {
			$a->{pos} cmp $b->{pos};
		} else {
			$a->{oname} cmp $b->{oname};
		}
	} @officers) {
	if ($lname ne $o->{oname}) {
		if (length($lname) > 0) {
			$ocmanable .= "\n";
		}
		$lname = $o->{oname};
	}
	$ocmanable .= sprintf $ofmt, $o->{pos}, $o->{oname}, $o->{aname}, $o->{desc};
}

if (!open(T,"templates/OFFICERS.txt")) {
	print "templates/OFFICERS.txt can't be opened, ugh\n";
	exit(1);
}
if (!open(O,">OFFICERS.txt")) {
	print "OFFICERS.txt can't be opened for writing, ugh\n";
	exit(1);
}

while(<T>) {
	chomp($l=$_);
	$l =~ s/%O_C_MANEAUVER_ABILITY%/${ocmanable}/;
	if ($l =~ /%(battleship|explorer|surveyor|interceptor)(\[.*\]%|%)/) {
		my @olist;
		my $ship = $1;
		my $list = $2;
		if (length($list) > 1) {
			my @flags;
			$list =~ s/[\[\]%]//g;
			if ($list =~ /,/) {
				@flags = split(/,/,$list);
			} else {
				@flags = ($list);
			}
			print O " ship=${ship} flags=";
			foreach my $f (@flags) {
				print O "${f},";
			}
			@olist = search_officers($ship, @flags);
		} else {
			print O " ship=${ship}";
			@olist = search_officers($ship);
		}
		print O "\n";
		foreach my $o (@olist) {
			printf O " ${ofmt}", $o->{pos}, $o->{oname}, $o->{aname}, $o->{desc};
		}
		next;
	}
	print O "${l}\n";
}
close(O);
close(T);

exit(0);

sub check_max {
	my ($o, $n) = @_;
	my $len = length($o->{$n});
	if (!defined($max->{$n})) {
		$max->{$n} = $len;
		return;
	}
	if ($max->{$n} < $len) {
		$max->{$n} = $len;
	}
}

sub search_officers {
	my (@searches) = @_;

	my @olist;

	my $matchinfo = { };

	my $scount = 0;
	while ($scount <= $#searches) {
		my $s = $searches[$scount++];
		foreach my $o (@officers) {
			my $oname = $o->{oname};
			my $pos   = $o->{pos};
			my $flagval = $o->{flags}->{$s};
			if (!defined($flagval)) {
				print "no flag $s found\n";
				next;
			}
			if ($o->{flags}->{$s} > 0) {
				my $oskip = 0;
				foreach my $f (@{$flagexcl->{$s}}) {
					@olist = grep { $_->{flags}->{$f} == 0 } @olist;
					my $sf = $o->{flags}->{$f};
					if (!defined($sf)) {
						next;
					}
					if ($sf > 0) {
						$oskip++;
					}
				}
				if ($oskip == 0) {
					if (defined($matchinfo->{$oname.$pos})) {
						next;
					}
					$matchinfo->{$oname.$pos} = 1;
					push @olist, $o;
				}
				next;
			}
		}
	}
	return @olist;
}
